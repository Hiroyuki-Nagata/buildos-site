<!DOCTYPE html>
<html lang="ja">
  <head>
    <meta charset="utf-8">
    <meta http-equiv="X-UA-Compatible" content="IE=edge">
    <meta name="viewport" content="width=device-width, initial-scale=1">
    <!-- The above 3 meta tags *must* come first in the head; any other head content must come *after* these tags -->
    <title>３４ー４２日目 - OS自作入門３日目のアセンブラ実装 <small></title>

    <!-- Bootstrap -->
    <link href="../css/bootstrap.min.css" rel="stylesheet">
    <link href="../css/syntaxhighlighter/shCore.css" rel="stylesheet">
    <link href="../css/syntaxhighlighter/shThemeDefault.css" rel="stylesheet">
    <link href="../css/overwrite.css" rel="stylesheet">

    <!-- HTML5 shim and Respond.js for IE8 support of HTML5 elements and media queries -->
    <!-- WARNING: Respond.js doesn't work if you view the page via file:// -->
    <!--[if lt IE 9]>
      <script src="https://oss.maxcdn.com/html5shiv/3.7.2/html5shiv.min.js"></script>
      <script src="https://oss.maxcdn.com/respond/1.4.2/respond.min.js"></script>
    <![endif]-->

    <!-- Google Analystics -->
    <script>
      (function(i,s,o,g,r,a,m){i['GoogleAnalyticsObject']=r;i[r]=i[r]||function(){
      (i[r].q=i[r].q||[]).push(arguments)},i[r].l=1*new Date();a=s.createElement(o),
      m=s.getElementsByTagName(o)[0];a.async=1;a.src=g;m.parentNode.insertBefore(a,m)
      })(window,document,'script','https://www.google-analytics.com/analytics.js','ga');

      ga('create', 'UA-76215716-1', 'auto');
      ga('send', 'pageview');
    </script>
  </head>
  <body>

    <!-- navbar -->
    <header>
      <div class="navbar navbar-default navbar-fixed-top">
	<div class="container">
	  <div class="navbar-header">
            <a href="../index.html" class="navbar-brand">OS自作入門メモ</a>
            <button class="navbar-toggle" type="button" data-toggle="collapse" data-target="#navbar-main">
              <span class="icon-bar"></span>
              <span class="icon-bar"></span>
              <span class="icon-bar"></span>
            </button>

	    <div class="navbar-collapse collapse" id="navbar-main">
	      <ul class="nav navbar-nav">
		<script async src="//pagead2.googlesyndication.com/pagead/js/adsbygoogle.js"></script>
		<!-- OS自作入門 -->
		<ins class="adsbygoogle"
		     style="display:inline-block;width:728px;height:15px"
		     data-ad-client="ca-pub-9555568254794680"
		     data-ad-slot="9016409597"></ins>
		<script>
		  (adsbygoogle = window.adsbygoogle || []).push({});
		</script>
	      </ul>
	    </div>
	  </div>
	</div>
      </div>
    </header>

    <!-- Containers -->
    <div class="container">
      <div class="bs-docs-section">
	<!-- panel -->
	<div class="row">
	  <div class="col-lg-12">
            <div class="bs-component">
              <div class="panel panel-primary">
		<div class="panel-heading">
		  <h3 class="panel-title"><strong>３４ー４２日目</strong> - OS自作入門３日目のアセンブラ実装 <small>2016/5/25, 27, 30, 31, 2016/6/1, 2, 4, 6, 7</small></h3>
		</div>


		<div class="panel-body" id="accordion">
		  <!-- START -->

		  <h2>OS自作入門2, ３日目のアセンブラをダンプする</h2>

		  <h3>2日目のアセンブラ</h3>
		  <p>アセンブラを実装する上では、この２日目のコードが一番の難関でした。</p>
		  <p>主なオペコードたち</p>
		  <ul>
		    <li><strong>JMP</strong>：以下の部分に注目して欲しいです。<br>
		      <strong>JMP entry</strong>と、アセンブラが書かれています。これは次のentryというラベルまで78byteあることを指しています。</li>
		    <pre>        JMP     entry           ; eb4e
                                ; 上記 0x4e(78d, 4e自身から見て次のentryまで78byteある)</pre>

		    <li><strong>JMP</strong>：次に以下の部分に注目して欲しいです。<br>
		    <strong>putloop: </strong>というラベルにジャンプする必要があるので <strong>JMP putloop</strong>と書くと-18byteと指し示すことになります。<li>
		      <pre>
putloop:
        MOV     AL,[SI]         ; 8a04
        ADD     SI,1            ; 83c601
        CMP     AL,0            ; 3c00
        JE      fin             ; 7409
        MOV     AH,0x0e         ; b40e
        MOV     BX,15           ; bb0f00
        INT     0x10            ; cd10
        JMP     putloop         ; ebee => 0xee(-18d) ; 符号あり1byteの0xeeは10進数で-18です。ここで試してみて → http://hogehoge.tk/tool/number.html</pre></li>

                    <li>リンク先で<strong>rel8, rel16, rel32</strong>という表記で示される相対オフセットとは、つまりはそういうことです。</li>

		    <li>参考：<a href = "http://softwaretechnique.jp/OS_Development/Tips/IA32_instructions.html">Tips　IA32（x86）汎用命令一覧</a></li>
		    <li>参考：<a href = "http://hogehoge.tk/tool/number.html">2進数、8進数、10進数、16進数相互変換ツール</a></li><br>

		    <li><strong>MOV</strong>：この表記も覚えておくべきでしょう。<br>
		      <strong>MOV AL,[SI]</strong>と、アセンブラが書かれています。これは[SI]がレジスタではなくメモリであることを示しています。<br>
		      メモリにあるか、レジスタにあるか、それとも即値かでニーモニックは大きく変化します。<br>
		      詳しくは"80x86 Opcodes"のサイトにあるMOV命令のニーモニック変化表を見てください。大体のところ、<br>

		      <pre>
Reg: レジスタ
Mem: メモリ
Acc: アキュムレータ
Imm: 即値
Seg: セグメントレジスタ</pre>

		      ってな具合です。この辺についてはOS自作入門にも解説されていますね。（流石にその実装方法までは書かれてませんが…）
		    </li>
		    <pre>        MOV     AL,[SI]         ; 8a04</pre>

		    <li>参考：<a href = "https://courses.engr.illinois.edu/ece390/resources/opcodes.html">80x86 Opcodes</a></li>


		  </ul>

<pre class="brush:cpp; collapse:true;" title="helloos.nas">
; hello-os
; TAB=4

        ORG     0x7c00          ; ORGはバイナリには現れない

; 以下は標準的なFAT12フォーマットフロッピーディスクのための記述

        JMP     entry           ; eb4e
                                ; 上記 0x4e(78d, 4e自身から見て次のentryまで78byteある)
        DB      0x90            ; 90
        DB      "HELLOIPL"      ; 48454c4c4f49504c
        DW      512             ; 0002
        DB      1               ; 01
        DW      1               ; 0100
        DB      2               ; 02
        DW      224             ; e000
        DW      2880            ; 400b
        DB      0xf0            ; f0
        DW      9               ; 0900
        DW      18              ; 1200
        DW      2               ; 0200
        DD      0               ; 00000000
        DD      2880            ; 400b0000
        DB      0,0,0x29        ; 000029
        DD      0xffffffff      ; ffffffff
        DB      "HELLO-OS   "   ; 48454c4c4f2d4f53202020
        DB      "FAT12   "      ; 4641543132202020
        RESB        18          ; 000000000000000000000000000000000000

; プログラム本体

entry:
        MOV     AX,0            ; b80000
        MOV     SS,AX           ; 8ed0
        MOV     SP,0x7c00       ; bc007c
        MOV     DS,AX           ; 8ed8
        MOV     ES,AX           ; 8ec0

        MOV     SI,msg          ; be747c
putloop:
        MOV     AL,[SI]         ; 8a04
        ADD     SI,1            ; 83c601
        CMP     AL,0            ; 3c00
        JE      fin             ; 7409
        MOV     AH,0x0e         ; b40e
        MOV     BX,15           ; bb0f00
        INT     0x10            ; cd10
        JMP     putloop         ; ebee => 0xee(-18d)
fin:
        HLT                     ; f4
        JMP     fin             ; ebfd

msg:
        DB      0x0a, 0x0a      ; 0a0a
        DB      "hello, world"  ; 68656c6c6f2c20776f726c64
        DB      0x0a            ; 0a
        DB      0               ; 00
        RESB    0x7dfe-$        ; 0x7dfeまで0x00
        DB      0x55, 0xaa      ; 55aa

; 後はまあいいですよね…

        DB      0xf0, 0xff, 0xff, 0x00, 0x00, 0x00, 0x00, 0x00
        RESB    4600
        DB      0xf0, 0xff, 0xff, 0x00, 0x00, 0x00, 0x00, 0x00
        RESB    1469432
</pre>

		  <h3>3日目のアセンブラ</h3>
		  <p>実は３日目の最初はあまり新出のオペコードがないです。リンク先を参照すればすべて実装できます。</p>

		  <p>３日目のアセンブラで新出のオペコード</p>
		  <ul>
		    <li><strong>JAE,</strong>
		    <strong>JNC,</strong>
		    <strong>JC,</strong>
		    <strong>JE </strong></li>
		  </ul>
<p>参考：<a href = "http://softwaretechnique.jp/OS_Development/Tips/IA32_Instructions/Jcc.html">Jcc命令（Jump If Condition Is Met）</a></p>

<pre class="brush:cpp; collapse:true;" title="harib00a">
; haribote-ipl
; TAB=4

        ORG     0x7c00          ; ORGはバイナリには現れない

; 以下は標準的なFAT12フォーマットフロッピーディスクのための記述

        JMP     entry           ; eb4e
        DB      0x90            ; 90
        DB      "HARIBOTE"      ; 48415249424f5445
        DW      512             ; 0002
        DB      1               ; 01
        DW      1               ; 0100
        DB      2               ; 02
        DW      224             ; e000
        DW      2880            ; 400b
        DB      0xf0            ; f0
        DW      9               ; 0900
        DW      18              ; 1200
        DW      2               ; 0200
        DD      0               ; 00000000
        DD      2880            ; 400b0000
        DB      0,0,0x29        ; 000029
        DD      0xffffffff      ; ffffffff
        DB      "HARIBOTEOS "   ; 48415249424f54454f5320
        DB      "FAT12   "      ; 4641543132202020
        RESB    18              ; 000000000000000000000000000000000000

; プログラム本体

entry:
        MOV     AX,0            ; b80000
        MOV     SS,AX           ; 8ed0
        MOV     SP,0x7c00       ; bc007c
        MOV     DS,AX           ; 8ed8   // ここまでは同じ

; ディスクを読む

        MOV     AX,0x0820       ; b82008
        MOV     ES,AX           ; 8ec0
        MOV     CH,0            ; b500
        MOV     DH,0            ; b600
        MOV     CL,2            ; b102

        MOV     AH,0x02         ; b402
        MOV     AL,1            ; b001
        MOV     BX,0            ; bb0000
        MOV     DL,0x00         ; b200
        INT     0x13            ; cd13
        JC      error           ; 7203

; 読み終わったけどとりあえずやることないので寝る

fin:
        HLT                     ; f4
        JMP     fin             ; ebfd

error:
        MOV     SI,msg          ; be8a7c
putloop:
        MOV     AL,[SI]         ; 8a04
        ADD     SI,1            ; 83c601
        CMP     AL,0            ; 3c00
        JE      fin             ; 74f1
        MOV     AH,0x0e         ; b40e
        MOV     BX,15           ; bb0f00
        INT     0x10            ; cd10
        JMP     putloop         ; ebee
msg:
        DB      0x0a, 0x0a      ; 0a0a
        DB      "load error"    ; 6c6f6164206572726f72
        DB      0x0a            ; 0a
        DB      0               ; 00

        RESB    0x7dfe-$        ; 0x7dfeまで0x00

        DB      0x55, 0xaa      ; 55aa
</pre>

<pre class="brush:cpp; collapse:true;" title="harib00b">
; haribote-ipl
; TAB=4

        ORG     0x7c00          ; ORGはバイナリには現れない

; 以下は標準的なFAT12フォーマットフロッピーディスクのための記述

        JMP     entry           ; eb4e
        DB      0x90            ; 90
        DB      "HARIBOTE"      ; 48415249424f5445
        DW      512             ; 0002
        DB      1               ; 01
        DW      1               ; 0100
        DB      2               ; 02
        DW      224             ; e000
        DW      2880            ; 400b
        DB      0xf0            ; f0
        DW      9               ; 0900
        DW      18              ; 1200
        DW      2               ; 0200
        DD      0               ; 00000000
        DD      2880            ; 400b0000
        DB      0,0,0x29        ; 000029
        DD      0xffffffff      ; ffffffff
        DB      "HARIBOTEOS "   ; 48415249424f54454f5320
        DB      "FAT12   "      ; 4641543132202020
        RESB    18              ; 000000000000000000000000000000000000

; プログラム本体

entry:
        MOV     AX,0            ; b80000
        MOV     SS,AX           ; 8ed0
        MOV     SP,0x7c00       ; bc007c
        MOV     DS,AX           ; 8ed8

; ディスクを読む

        MOV     AX,0x0820       ; b82008
        MOV     ES,AX           ; 8ec0
        MOV     CH,0            ; b500
        MOV     DH,0            ; b600
        MOV     CL,2            ; b102

        MOV     SI,0            ; be0000
retry:
        MOV     AH,0x02         ; b402
        MOV     AL,1            ; b001
        MOV     BX,0            ; bb0000
        MOV     DL,0x00         ; b200
        INT     0x13            ; cd13
        JNC     fin             ; 7310
        ADD     SI,1            ; 83c601
        CMP     SI,5            ; 83fe05
        JAE     error           ; 730b
        MOV     AH,0x00         ; b400
        MOV     DL,0x00         ; b200
        INT     0x13            ; cd13
        JMP     retry           ; ebe3

; 読み終わったけどとりあえずやることないので寝る

fin:
        HLT                     ; f4
        JMP     fin             ; ebfd

error:
        MOV     SI,msg          ; be9d7c
putloop:
        MOV     AL,[SI]         ; 8a04
        ADD     SI,1            ; 83c601
        CMP     AL,0            ; 3c00
        JE      fin             ; 74f1
        MOV     AH,0x0e         ; b40e
        MOV     BX,15           ; bb0f00
        INT     0x10            ; cd10
        JMP     putloop         ; ebee
msg:
        DB      0x0a, 0x0a      ; 0a0a
        DB      "load error"    ; 6c6f6164206572726f72
        DB      0x0a            ; 0a
        DB      0               ; 00

        RESB    0x7dfe-$        ; 0x7dfeまで0x00

        DB      0x55, 0xaa      ; 55aa
</pre>

		  <!-- END -->

		  <ul class="pager">
		    <li><a href="./day28-33.html">前の日</a></li>
		    <li><a href="#">次の日</a></li>
		  </ul>
		</div>
              </div>
            </div>
	  </div>
	</div>
      </div>
    </div>

    <!-- jQuery (necessary for Bootstrap's JavaScript plugins) -->
    <script src="https://ajax.googleapis.com/ajax/libs/jquery/1.11.2/jquery.min.js"></script>
    <!-- Include all compiled plugins (below), or include individual files as needed -->
    <script src="../js/bootstrap.min.js"></script>
    <script src="../js/syntaxhighlighter/scripts/shCore.js"></script>
    <script src="../js/syntaxhighlighter/scripts/shBrushCpp.js"></script>
    <script src="../js/syntaxhighlighter/scripts/shBrushBash.js"></script>
    <script type="text/javascript">
      SyntaxHighlighter.config.clipboardSwf = '../js/syntaxhighlighter/scripts/clipboard.swf';
      SyntaxHighlighter.all();
    </script>
  </body>
</html>
